(* 
    Syntactic Grammer Definition
    
    Notation is homebrew BNF (see bnf.sublime-syntax for highlighting).
    
    I plan to implement a recursive descent parser, so care needs to be taken to avoid any left recursion in the grammar.
*)

(*** Type Annotations ***)

type-expression ::= (* TODO *) ;


(*** Statements ***)

statement ::= ";"
            | while-loop
            | do-while-loop
            | for-loop 
            | expression ;

statement-list ::= ( statement )* ( control-flow )? ;  (* control flow only allowed at end of block, much like Lua *)
control-flow ::= "continue" ( label )? | "break" ( label )? ( expression )? | "return" ( expression )? ;

while-loop ::=  ( label )? "while" expression "do" statement-list "end" ;
do-while-loop ::= ( label )? "do" statement-list "while" expression "end" ;

for-loop ::= ( label )? "for" lvalue-list "in" expression "end" ;

label ::= "::" LABELNAME ;

(*** Expressions ***)

(* 
    This expression production is left-recursive, and as such, the actual recursive descent parser implementation differs somewhat (see the bottom of this file)
*)
expression ::= primary
             | class-def
             | function-def
             | if-expression
             | block-expression
             | UNARY_OP expression
             | expression BINARY_OP expression
             | assignment-expression
             | declaration-expression
             | object-constructor
             | tuple-constructor ;

expression-list ::= expression ( "," expression )* ;

(*** Primary expressions ***)

atom ::= LITERAL | identifier | "self" | "super" | group ;
identifier ::= IDENTIFIER | "global" IDENTIFIER | "upval" IDENTIFIER ;

primary ::= atom ( access-item )* ; 
access-item ::= member-access | index-access | invocation | object-constructor ;
index-access ::= "[" expression "]" ;
member-access ::= "." IDENTIFIER ;

group ::= "(" expression ( ":" type-expression )? ")" ; (* can be type annotated *)

(*** if/block Expressions ***)

if-expression ::= "if" expression "then" statement-list ( "elseif" statement-list )* ( "else" statement-list )? "end" ;
block-expression ::= ( label )? "begin" ( statement )* ( control-flow )? "end" ;  (* break can be supplied a value inside of begin-blocks *)

(*** Assignment ***)

(* 
    The "var" keyword creates the variable in current scope, otherwise the global scope is searched for an 
    existing variable if cannot be not found locally. If it still can't be found, then the assignment is an error.
    
    Assignment statements support sequence destructuring, assignment expressions do not.
*)

(* right associative *)
lvalue ::= identifier | primary index-access | primary member-access ;

lvalue-expression ::= lvalue | lvalue-list | "(" lvalue ")" ;   (* basically just lvalues, and tuples of lvalues *)
lvalue-list ::= lvalue-expression ( "," lvalue-expression )* ;

lvalue-annotated ::= lvalue-expression ( ":" type-expression )? ; 

assignment-op ::= "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>=" ;
assignment-expression ::= lvalue-annotated assignment-op expression ;

declaration-expression ::= ( "let" | "var" ) assignment-expression ;



(*** Function/Method Calls ***)

(* 
Special case here: An invocation cannot be the first production following a new line. 
This is the only situation where white space is considered when parsing RLO.
This prevents function calls from splitting across lines (an issue that Lua also has, and our syntax is based on Lua).
However RLO has tuples, so its important for us to deal with that.
*)
invocation ::= "(" ")" | "(" argument ( "," argument )* ")" ; 
argument ::= ( "..." )? expression ;  (* "..." is for argument unpacking syntax *)

(*** Constructors ***)

object-constructor ::= "{" member-initializer ( "," member-initializer )* "}" ;
member-initializer ::= ( IDENTIFIER | "[" primary "]" ) ( ":" type-expression )? "=" expression ;


(* syntax for tuple-constructor has special casing for single-element tuples and the empty tuple *)
tuple-constructor ::= expression-list | "(" expression "," ")" | "(" ")" ;

(*** Function Defs ***)

function-def ::= "fun" ( lvalue )?  parameter-list ( "->" type-expression ":" )?  statement-list "end" ;

parameter-list ::= "(" ")" | "(" parameter ( "," parameter )* ( parameter "..." )? ")" ;

parameter ::= ( "var" )? IDENTIFIER ( ":" type-expression )? ( "=" expression )? ;


(*** Class Defs ***)

class-def ::= (* TODO *) ;


(***** Non left-recursive version of `expression` for implementation *****)

(* Single-element tuple literals are not allowed. Use a built-in constructor function to create single element tuples *)
tuple-constructor ::= expression ( "," expression )+ | "(" ")" ;

primary-expression ::= immediate-expression | primary | tuple-constructor | "(" expression ")" ;

(* These expressions can all be identified by their first symbol *)
immediate-expression ::= class-def
                       | function-def
                       | if-expression
                       | object-constructor ;

unary-expression ::= UNARY_OP unary-expression | primary-expression ;

(*
    Operator precendence in order from strong to weak binding
    (left associative unless otherwise specified)
     
     1:  (unary -) (unary +) not          (right associative)
     2:  * / %
     3:  + - 
     4:  << >>
     5:  & 
     6:  ^ 
     7:  | 
     8:  < > <= >= == !=
     9:  and 
    10:  or

*)

operand[1] ::= unary-expression ;
operand[N] ::= operand[N-1] ( OPERATOR[N] operand[N-1] )* ;
binary-op ::= operand[10] ;

(* top-level tuples don't require parens - however, single element tuples are not allowed here, use tuple-constructor instead *)
naked-tuple ::= binary-op ( "," binary-op )*;

assignment ::= naked-tuple | assignment-expression | declaration-expression ;

expression ::= assignment (* top level of recursive descent *)
